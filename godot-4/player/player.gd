class_name Player extends CharacterBody2D

const SPEED = 100.0
const ROLL_SPEED = 125.0

var input_vector: = Vector2.DOWN
var last_input_vector: = Vector2.ZERO

@export var stats: Stats

@onready var animation_tree: AnimationTree = $AnimationTree
@onready var playback = animation_tree.get("parameters/StateMachine/playback") as AnimationNodeStateMachinePlayback
@onready var hurtbox: Hurtbox = $Hurtbox
@onready var blink_animation_player: AnimationPlayer = $BlinkAnimationPlayer

func _ready() -> void:
	hurtbox.hurt.connect(_take_hit.call_deferred)
	stats.no_health.connect(_die)

func _physics_process(delta: float) -> void:
	var state = playback.get_current_node()
	input_vector = Input.get_vector("move_left", "move_right", "move_up", "move_down").normalized();

	match state:
		"MoveState": _move_state(delta)
		"AttackState": pass
		"RollState": _roll_state(delta)

func _move_state(delta: float) -> void:
	if input_vector != Vector2.ZERO:
		last_input_vector = input_vector
		_animate()

	if Input.is_action_just_pressed("attack"):
		playback.travel("AttackState")

	if Input.is_action_just_pressed("roll"):
		playback.travel("RollState")

	velocity = input_vector * SPEED
	move_and_slide()

func _roll_state(delta: float) -> void:
	velocity = last_input_vector * ROLL_SPEED
	move_and_slide()

func _animate():
	var fixed_input = Vector2(input_vector.x, -input_vector.y)
	animation_tree.set("parameters/StateMachine/AttackState/blend_position", fixed_input)
	animation_tree.set("parameters/StateMachine/RollState/blend_position", fixed_input)
	animation_tree.set("parameters/StateMachine/MoveState/RunState/blend_position", fixed_input)
	animation_tree.set("parameters/StateMachine/MoveState/StandState/blend_position", fixed_input)

func _take_hit(other_hitbox: Hitbox):
	stats.health -= other_hitbox.damage
	blink_animation_player.play("hit")

func _die() -> void:
	hide()
	remove_from_group("player")
	process_mode = Node.PROCESS_MODE_DISABLED
